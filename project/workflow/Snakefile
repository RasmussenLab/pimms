"""
Document how all the notebooks for a single experiment are connected.
"""
from snakemake.logging import logger


configfile: "config/single_dev_dataset/proteinGroups/config.yaml"


folder_experiment = config["folder_experiment"]


logger.info(f"{folder_experiment = }")


rule all:
    input:
        f"{folder_experiment}/figures/performance_methods_by_completness.pdf",


nb = "01_2_performance_plots.ipynb"


rule comparison:
    input:
        nb=nb,
        runs=expand(
            "{folder_experiment}/01_1_train_{model}.ipynb",
            folder_experiment=config["folder_experiment"],
            model=config["models"],
        ),
    output:
        pdf="{folder_experiment}/figures/performance_methods_by_completness.pdf",
        nb="{folder_experiment}" f"/{nb}",
    params:
        meta_data=config["fn_rawfile_metadata"],
        models=",".join(config["models"]),
    shell:
        "papermill {input.nb} {output.nb}"
        " -r fn_rawfile_metadata {params.meta_data:q}"
        " -r folder_experiment {wildcards.folder_experiment:q}"
        " -r models {params.models:q}"
        " && jupyter nbconvert --to html {output.nb}"


# separate workflow by level -> provide custom configs
nb = "01_0_split_data.ipynb"


rule create_splits:
    input:
        nb=nb,
        configfile=config["config_split"],
    output:
        train_split="{folder_experiment}/data/train_X.pkl",
        nb="{folder_experiment}" f"/{nb}",
    params:
        folder_experiment="{folder_experiment}",
    shell:
        "papermill {input.nb} {output.nb}"
        " -f {input.configfile}"
        " -r folder_experiment {params.folder_experiment}"
        " && jupyter nbconvert --to html {output.nb}"


rule train_models:
    input:
        nb="01_1_train_{model}.ipynb",
        train_split="{folder_experiment}/data/train_X.pkl",
        configfile=config["config_train"],
    output:
        nb="{folder_experiment}/01_1_train_{model}.ipynb",
    params:
        folder_experiment="{folder_experiment}",
    shell:
        "papermill {input.nb} {output.nb}"
        " -f {input.configfile}"
        " -r folder_experiment {params.folder_experiment}"
        " -r model_key {wildcards.model}"
        " && jupyter nbconvert --to html {output.nb}"
