"""
Incremental development:

1) run on local machine in dry-run (iteratively adding functionality)
2) execute using `qsub` (or interactive shell on CR2)
3) add queuing system `qsub`
"""
import os
from pathlib import Path
configfile: 'config.yaml'  # access using config['key']
MIN_FILE_SIZE = 900_000_000

folder_logs = Path(config['LOGDIR'])
results_foldername = os.path.basename(config["RESULTSDIR"])

# only needed if login to remote is not set up using ssh keys
# move away from config into envvars
# envvars:
#     "SSHPASS"

# wildcard_mqpar = os.path.join(config['SCRIPTDIR'], 'mqpar_xmls', "mqpar_{file}.xml")
# mq_par_temp = os.path.join(config['SCRIPTDIR'], config['MQ_PAR_TEMP'])

print(f"Use files from: {config['FILES']}")
with open(config['FILES'], encoding='utf-8') as f:
    FILES = set(line.strip().split('.raw')[0] for line in f)

# Order Files (set gives an unordered)
FILES = list(FILES)
FILES.sort()  # otherwise execution order is not deterministic due to set
# local, excluded files previously identified as to small
fname_excluded = folder_logs / \
    f"{results_foldername}_excluded_files.txt"
print(f"Exclude files from: {fname_excluded}")
try:
    with open(fname_excluded, encoding='utf-8') as f:
        FILES_EXCLUDED = set(line.strip().split('.raw')[0] for line in f)
except FileNotFoundError:
    print(f"No such file: {fname_excluded} - Creating one.")
    with open(fname_excluded, 'w'):
        pass
    FILES_EXCLUDED = []
for _file_to_remove in FILES_EXCLUDED:
    try:
        FILES.remove(_file_to_remove)
    except ValueError as e:
        print(f"WARNING: File to exclude not in inputs: {_file_to_remove}")


# local rules are excuted in the process (job) running snakemake
# allows for ssh-multiplexing, see https://blog.scottlowe.org/2015/12/11/using-ssh-multiplexing/
localrules: target, mqpar, download_file, upload_file


# Thinnode resources sharing: 40 cores and 196 GB RAM (minus 2GB for snakemake)
# http://www.dewassoc.com/kbase/hard_drives/binary_v_decimal_measurement.htm
job_ram_mb = int(204_800 / 40 * config['THREATS_MQ'])

# #Target Rule:
rule target:
    input:
        # mockfile approach: https://stackoverflow.com/a/53751654/9684872
        # replace? https://snakemake.readthedocs.io/en/stable/snakefiles/rules.html#directories-as-outputs
        expand(os.path.join(config['RESULTSDIR'],
                            "{file}", "{file}.txt"), file=FILES)

# File size could be done on remote before downloading, only security check afterwards?s
rule download_file:
    output:
        raw = temp(os.path.join(config['DATADIR'], "{file}", "{file}.raw"))
    benchmark:
        os.path.join(config['RESULTSDIR'], "{file}", "benchmark_download.tsv")
    params:
        file = "{file}.raw",
        logdir = os.path.join(config['RESULTSDIR'], "{file}"),
        remote = config['REMOTE_IN'],  # SSHPASS migt be used here
        datadir = os.path.join(config['DATADIR'], "{file}"),
        min_file_size = MIN_FILE_SIZE
    resources:
        walltime = 300, nodes = 1, mem_mb = 2048
    threads:
        1
    shell:
        """
        sftp -B 258048 {params.remote} <<< 'get {params.file} {output.raw}' && (
        FILESIZE=$(stat -c%s {output.raw}) &&
        [ $FILESIZE -le {params.min_file_size} ] &&
        echo '{params.file}' >> {fname_excluded} &&
        echo "{params.file} is too small: $FILESIZE" &&
        echo "File is too small: $FILESIZE" > {params.logdir}/download_too_small.log &&
        rm -R {params.datadir} &&
        exit 1 ||  exit 0 )
        """

# Is it possible to have relative paths for the Output?
# Path('mqpar_xmls') / 'mq_par_{file}.xml'
#
# should it be a temporary file, otherwise THREADS_MQ is fixed?
rule mqpar:
    benchmark:
        Path(config['RESULTSDIR']) / "{file}" / "benchmark_mqpar.tsv"
    params:
        raw = os.path.join(config['DATADIR'], "{file}", "{file}.raw"),
        mq_par_temp = os.path.join(config['SCRIPTDIR'], config['MQ_PAR_TEMP']),
        threads_mq = config['THREATS_MQ'],
        logdir = os.path.join(config['RESULTSDIR'], "{file}"),
    resources:
        walltime = 240, nodes = 1, mem_mb = 1024
    threads:
        1
    output:
        mq_par = Path(config['SCRIPTDIR']) / 'mqpar_xmls' / "mqpar_{file}.xml"
    run:
        # snakemake create folders itself if missing -> download job into folder
        #
        # multi-line with statements natively supported from Python 3.9:
        # https://stackoverflow.com/questions/31039022/python-multi-line-with-statement
        with open(file=params.mq_par_temp, encoding='utf-8') as infile, open(file=output.mq_par, mode='w', encoding='utf-8') as outfile:
            for line in infile:
                line = line.replace('PATH', params.raw)
                line = line.replace('NUM_THREADS', str(params.threads_mq))
                outfile.write(line)
            outfile.close()
            infile.close()


rule maxquant:
    input:
        raw = os.path.join(config['DATADIR'], "{file}", "{file}.raw"),
        mq_par = os.path.join(config['SCRIPTDIR'],
                              'mqpar_xmls', "mqpar_{file}.xml"),
        exe = config['MAXQUANTEXE']
    benchmark:
        os.path.join(config['RESULTSDIR'], "{file}", "benchmark_MQ.tsv")
    output:
        out = os.path.join(config['RESULTSDIR'],
                           "{file}", "{file}_mq_done.txt")
    resources:
        mem_mb = job_ram_mb, nodes = 1, walltime = '5:00:00'
    threads:
        config['THREATS_MQ']
    envmodules:
        "tools",
        "mono/5.20.1.19"
    params:
        datadir = os.path.join(config['DATADIR'], "{file}"),
        logdir = os.path.join(config['RESULTSDIR'], "{file}"),
        log_failed = f"{results_foldername}_failed.txt",
        log_completed = folder_logs / f"{results_foldername}_completed.txt",
        remote = config['REMOTE_OUT'],
        min_file_size = MIN_FILE_SIZE
    shell:
        # https://snakemake.readthedocs.io/en/stable/project_info/faq.html#i-don-t-want-snakemake-to-detect-an-error-if-my-shell-command-exits-with-an-exitcode-1-what-can-i-do
        # write shell script?
        # https: // stackoverflow.com/a/27301889/9684872
        # changing in presence of here-documents (<< EOF EOF below)
        # https://snakemake.readthedocs.io/en/stable/project_info/faq.html#is-it-possible-to-pass-variable-values-to-the-workflow-via-the-command-line
        # { have to be escaped as {{
        """
        mono --version &&
        FILESIZE=$(stat -c%s {input.raw}) &&
        [ $FILESIZE -ge {params.min_file_size} ] &&
        mono {input.exe} {input.mq_par} &&
        echo "$(date +"%F %T"): Finished run with MaxQuant version {input.exe}" >> {output.out} &&
        echo '-mkdir mq_out/{wildcards.file}' >> sftp_commands &&
        echo '-put {params.datadir}/combined/txt/* mq_out/{wildcards.file}' >> sftp_commands &&
        cp {input.mq_par} {params.datadir}/combined/txt/mqpar.xml &&
        echo '{wildcards.file}' >> {params.log_completed} ||
        ( echo '{wildcards.file}' >> {params.log_failed} &&
        echo "Size of {input.raw}: $FILESIZE" &&
        rm -R {params.datadir} &&
        exit 1 )
        """

rule upload_file:
    input:
        mq_done = os.path.join(
            config['RESULTSDIR'], "{file}", "{file}_mq_done.txt")
    output:
        out = os.path.join(config['RESULTSDIR'],
                           "{file}", "{file}.txt")
    resources:
        mem_mb = job_ram_mb, nodes = 1, walltime = 900
    params:
        datadir = os.path.join(config['DATADIR'], "{file}"),
        remote = config['REMOTE_OUT']
    shell:
        """
        sftp -B 258048 {params.remote} <<< '-mkdir mq_out/{wildcards.file}\nput {params.datadir}/combined/txt/* mq_out/{wildcards.file}' &&
        rm -R {params.datadir} &&
        echo 'Done uploading to mq_out/{wildcards.file} on {params.remote}' &&
        echo "$(date +"%F %T"): Finished upload to erda." >> {output.out} ||
        echo 'Failed upload for {wildcards.file}'
        """
